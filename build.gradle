plugins {
    id "java"
    id "idea"
    id "net.minecraftforge.gradle" version "[6.0,6.2)"
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id "com.modrinth.minotaur" version "2.+"
    id 'net.darkhax.curseforgegradle' version '1.+'
}

version = "${project.mod_version}"
group = "${project.mod_namespace}"
base {
    archivesName = "${project.mod_name}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {
    mappings channel: 'parchment', version: "2023.06.26-1.20.1"

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'

            mods {
                miniaturepowerplant {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run_server')
            property 'forge.logging.console.level', 'debug'
            property 'java.net.preferIPv6Addresses', 'false'


            mods {
                miniaturepowerplant {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run_data')
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'miniaturepowerplant', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                miniaturepowerplant {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url = "https://maven.blamejared.com/"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.forge_version}"
    implementation fg.deobf("mezz.jei:jei-${project.jei_version}")
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
def resourceTargets = ['META-INF/mods.toml']
def replaceProperties = [
        mod_version: "${project.mod_version}"
]
processResources {
    inputs.properties replaceProperties
    replaceProperties.put 'project', project

    filesMatching(resourceTargets) {
        expand replaceProperties
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title": "${project.mod_name}",
                "Specification-Vendor": "canitzp",
                "Specification-Version": "1",
                "Implementation-Title": "${project.mod_name}",
                "Implementation-Version": "${project.mod_version}",
                "Implementation-Vendor" :"canitzp",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

idea {
    module {
        inheritOutputDirs = true
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "roZbVc2D"
    versionNumber = "${project.mod_version}"
    versionName = "${project.mod_name} - ${project.mod_version} (for ${project.mc_version})"
    versionType = "release"
    uploadFile = jar
    gameVersions = project.release_minecraft_versions.split("/").collect()
    loaders = project.release_mod_loaders.toLowerCase().split("/").collect()
    changelog = String.join("\n", rootProject.file("CHANGELOG.md").text.lines().takeWhile(s -> !s.isEmpty()).toList())
    syncBodyFrom = rootProject.file("README.md").text
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)

//noinspection UnnecessaryQualifiedReference
task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    group = "publishing"
    apiToken = System.getenv('CURSEFORGE_TOKEN')

    def mainFile = upload(561733, jar)
    mainFile.displayName = "${project.mod_name} - ${project.mod_version} (for ${project.mc_version})"
    mainFile.releaseType = "release"
    mainFile.changelogType = 'markdown'
    mainFile.changelog = file("CHANGELOG.md")
    for (final def modloader in "${project.release_mod_loaders}".split("/")) {
        mainFile.addModLoader("${modloader}")
    }
    for (final def java_version in "${project.release_java_versions}".split("/")) {
        mainFile.addJavaVersion("${java_version}")
    }
    for (final def minecraft_version in "${project.release_minecraft_versions}".split("/")) {
        mainFile.addGameVersion("${minecraft_version}")
    }
}